private ["_object","_worldspace","_class","_donotusekey","_keySelected","_activatingPlayer","_isOK","_characterID","_dir","_location","_uid","_key","_result","_outcome","_continue","_object_para"];

_object             = _this select 0;
_worldspace         = _this select 1;
_class                 = _this select 2;
_donotusekey         = _this select 3;
_keySelected         = _this select 4;
_activatingPlayer    = _this select 5;

if(_donotusekey) then {
    _isOK = true;
} else {
    _isOK = isClass(configFile >> "CfgWeapons" >> _keySelected);
};

if(!_isOK) exitWith { diag_log ("HIVE: CARKEY DOES NOT EXIST: "+ str(_keySelected));  };

if(_donotusekey) then {
    _characterID = _keySelected;
} else {
    _characterID = str(getNumber(configFile >> "CfgWeapons" >> _keySelected >> "keyid"));
};

_dir         = _worldspace select 0;
_location     = _worldspace select 1;
_uid         = _worldspace call dayz_objectUID3;

_key = format["CHILD:308:%1:%2:%3:%4:%5:%6:%7:%8:%9:",dayZ_instance,_class,0,_characterID,_worldspace,[],[],1,_uid];
_key call server_hiveWrite;

_continue = false;

while {!_continue} do {
    _result        = nil;
    _key         = format["CHILD:388:%1:",_uid];
    _result     = _key call server_hiveReadWrite;
    _outcome     = _result select 0;

    if(_outcome == "PASS") then {
        _continue = true;
    } else {
        sleep 1;
    };
};

_oid = _result select 1;

deleteVehicle _object;

if(DZE_TRADER_SPAWNMODE) then {
    _object_para = createVehicle ["ParachuteMediumWest", [0,0,0], [], 0, "CAN_COLLIDE"];
    _object_para setPos [_location select 0, _location select 1,(_location select 2) + 65];
    _object = createVehicle [_class, [0,0,0], [], 0, "CAN_COLLIDE"];
} else {
    _object = createVehicle [_class,_location,[],0,"CAN_COLLIDE"];
};

if(!_donotusekey) then {
    _object setvehiclelock "locked";
};

clearWeaponCargoGlobal _object;
clearMagazineCargoGlobal _object;

//AN2_DZ add weapon - for all AN2-Types use "AN2_base" as classname
if (_object isKindOf "AN2_DZ") then {
    _object addWeapon "PKT"; //M240_veh
    _object setVehicleAmmo 0; //comment out if publish with ammo
};

_object setVariable ["ObjectID", _oid, true];    
_object setVariable ["lastUpdate",time];
_object setVariable ["CharacterID", _characterID, true];

if(DZE_TRADER_SPAWNMODE) then {
    _object attachTo [_object_para, [0,0,-1.6]];
    sleep 1.0;
    WaitUntil{(([_object] call FNC_GetPos) select 2) < 0.1};
    detach _object;
    deleteVehicle _object_para;
};

PVDZE_serverObjectMonitor set [count PVDZE_serverObjectMonitor,_object];

_object call fnc_veh_ResetEH;

PVDZE_veh_Init = _object;
publicVariable "PVDZE_veh_Init";
processInitCommands;

diag_log ("PUBLISH: " + str(_activatingPlayer) + " Bought " + (_class) + " with ID " + str(_uid));